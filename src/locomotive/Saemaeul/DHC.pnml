/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * DHC
 */
SPEED_GENERAL(DHC)
         COST(DHC)
      RUNNING(DHC)
     CAPACITY(DHC)
      LOADING(DHC)
// SPEED_GENERAL(DHC_wagon)
//          COST(DHC_wagon)
     CAPACITY(DHC_wagon)
      LOADING(DHC_wagon)

// DHC
spriteset(set_DHC_purchase, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_purchase_for_dualhead(0, 0)
}
spriteset(set_DHC1_engineH, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_loco_saemaeul(60, 0)
}
spriteset(set_DHC1_engineT, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_loco_saemaeul(60, 30)
}
spriteset(set_DHC2_engineH, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_loco_saemaeul(60, 60)
}
spriteset(set_DHC2_engineT, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_loco_saemaeul(60, 90)
}
spriteset(set_DHC3_engineH, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_loco_saemaeul(60, 120)
}
spriteset(set_DHC3_engineT, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_loco_saemaeul(60, 150)
}
spriteset(set_DHC1_wagon, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_wagon_by_coor(320, 0)
}
spriteset(set_DHC2_wagon, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_wagon_by_coor(320, 60)
}
spriteset(set_DHC3_wagon, "./src/locomotive/Saemaeul/DHC.png") {
    tmpl_wagon_by_coor(320, 120)
}

// Cafe car
// spriteset(set_DHC1_CAFE_CAR, "./src/wagon/Cafe/Cafe.png") {   // Not implemented yet. I'm not sure even it exists
//     tmpl_CAFE_CAR(, )
// }
// spriteset(set_DHC2_CAFE_CAR, "./src/wagon/Cafe/Cafe.png") {   // So does this
//     tmpl_CAFE_CAR(, )
// }
spriteset(set_DHC3_CAFE_CAR, "./src/wagon/Cafe/Cafe.png") {
    tmpl_CAFE_CAR(0, 60)
}

// Refit Paintings
switch(FEAT_TRAINS, SELF, sw_DHC_engineH, cargo_subtype) {
    1: set_DHC1_engineH;
    2: set_DHC2_engineH;
    set_DHC3_engineH;
}
switch(FEAT_TRAINS, SELF, sw_DHC_engineT, cargo_subtype) {
    1: set_DHC1_engineT;
    2: set_DHC2_engineT;
    set_DHC3_engineT;
}
switch(FEAT_TRAINS, SELF, sw_DHC_wagon, cargo_subtype) {
    1: set_DHC1_wagon;
    2: set_DHC2_wagon;
    set_DHC3_wagon;
}
switch(FEAT_TRAINS, SELF, sw_DHC_engine, position_in_consist_from_end == 0 && num_vehs_in_consist > 1) {
    1: sw_DHC_engineT;
    sw_DHC_engineH;
}
switch(FEAT_TRAINS, PARENT, sw_DHC_CAFE_CAR, cargo_subtype) {   // Cafe car
    // 1: set_CAFE_CAR_DHC1;
    // 2: set_CAFE_CAR_DHC2;
    set_DHC3_CAFE_CAR;
}
switch(FEAT_TRAINS, SELF, sw_DHC_subtype_text, cargo_subtype) {
    1: return string(STR_REFIT_PAINTING_1970_1983);
    2: return string(STR_REFIT_PAINTING_GREEN_YELLOW);
    return string(STR_REFIT_PAINTING_THESEDAYS);
}

// Attachable vehicles
switch(FEAT_TRAINS, SELF, sw_DHC_attach, vehicle_type_id) {
    ko_train_DHC:          return CB_RESULT_ATTACH_ALLOW;
    ko_train_ITX_SAEMAEUL: return CB_RESULT_ATTACH_ALLOW;
    ko_train_K4x00:        return CB_RESULT_ATTACH_ALLOW;
    ko_train_K7x00:        return CB_RESULT_ATTACH_ALLOW;
    ko_train_K7000:        return CB_RESULT_ATTACH_ALLOW;
    ko_train_K8000:        return CB_RESULT_ATTACH_ALLOW;
    ko_train_K8x00:        return CB_RESULT_ATTACH_ALLOW;
    ko_train_K8500:        return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWG:       return CB_RESULT_ATTACH_ALLOW;
    ko_train_SAEMAEUL_CAR: return CB_RESULT_ATTACH_ALLOW;
    ko_train_CAFE_CAR:     return CB_RESULT_ATTACH_ALLOW;
    return string(STR_ERROR_NO_ATTACH);
}

// Item
item(FEAT_TRAINS, ko_train_DHC, 5520) {
    property {
        // Common properties
        name:                           string(STR_DHC_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_DHC_INTRODUCTION;
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    1;

        // Spec
        speed:                          150 km/h;
        power:                          POWER_IN_KW(var_DHC_POWER);
        weight:                         69.71 ton;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
        visual_effect_and_powered:      VISUAL_EFFECT_DIESEL;
    }
    graphics {
        default:                        sw_DHC_engine;
        purchase:                       set_DHC_purchase;

        speed:                          sw_DHC_speed;
        cost_factor:                    sw_DHC_cost;
        running_cost_factor:            sw_DHC_runningcost;
        cargo_capacity:                 sw_DHC_capacity;
        loading_speed:                  sw_DHC_loadingspeed;

        can_attach_wagon:               sw_DHC_attach;
        cargo_subtype_text:             sw_DHC_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
    livery_override(ko_train_PASSWG) {
        default:                        sw_DHC_wagon;
        cargo_capacity:                 sw_DHC_wagon_capacity;
        loading_speed:                  sw_DHC_wagon_loadingspeed;
    }
    livery_override(ko_train_PASSWGP) {
        default:                        sw_DHC_wagon;
        cargo_capacity:                 sw_DHC_wagon_capacity;
        loading_speed:                  sw_DHC_wagon_loadingspeed;
    }
    livery_override(ko_train_CAFE_CAR) {
        default:                        sw_DHC_CAFE_CAR;
    }
}
// End of "DHC(PP)"