/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/

// Engine
switch(FEAT_TRAINS, SELF, sw_EUM_engine, position_in_consist_from_end == 0 && num_vehs_in_consist > 1) {
    1: set_EUM_engineT;
    set_EUM_engineH;
}

// Wagon
switch(FEAT_TRAINS, SELF, sw_EUM320_wagon_3, (position_in_vehid_chain + 1) % 4) {
    3: set_EUM_wagon_3_without_panto;
    set_EUM_wagon_3;
}
switch(FEAT_TRAINS, SELF, sw_EUM_wagon_3, cargo_subtype) {
    1: sw_EUM320_wagon_3;
    set_EUM_wagon_3;
}
switch(FEAT_TRAINS, SELF, sw_EUM_coach_by_free_tail, position_in_vehid_chain_from_end) {
    0: set_EUM_wagon_4;
    sw_EUM_wagon_3;
}
switch(FEAT_TRAINS, SELF, sw_EUM_power_wagon, position_in_vehid_chain) {
    0: set_EUM_wagon_2;
    sw_EUM_coach_by_free_tail;
}

// Capacity
switch(FEAT_TRAINS, SELF, var_EUM_capacity_express, 1) {
    return 46 / 2 * sw_cargo_capacity_factor;
}
switch(FEAT_TRAINS, SELF, var_EUM_capacity, 1) {
    return 76 / 2 * sw_cargo_capacity_factor;
}
switch(FEAT_TRAINS, SELF, sw_EUM_capacity_in_custom, position_in_consist) {
    0: return var_EUM_capacity_express;
    return var_EUM_capacity;
}

// Cost
switch(FEAT_TRAINS, SELF, sw_EUM_cost, cargo_subtype) {
    1: sw_EUM320_cost;
    return sw_EUM260_cost;
}

// Running cost
switch(FEAT_TRAINS, SELF, sw_EUM_runningcost, cargo_subtype) {
    1: sw_EUM320_runningcost;
    return sw_EUM260_runningcost;
}

// Speed
switch(FEAT_TRAINS, SELF, sw_EUM_speed, cargo_subtype) {
    1: sw_EUM320_speed;
    return sw_EUM260_speed;
}

// Power
switch(FEAT_TRAINS, SELF, sw_EUM_power, cargo_subtype) {
    1: return POWER_IN_KW(var_EUM320_POWER);
    return POWER_IN_KW(var_EUM260_POWER);
}

// Attach
switch(FEAT_TRAINS, SELF, sw_EUM_attach, vehicle_type_id) {
    ko_train_EUM:     return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWG:  return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWGP: return CB_RESULT_ATTACH_ALLOW;
    return string(STR_ERROR_NO_ATTACH);
}

// Refit
switch(FEAT_TRAINS, SELF, sw_EUM_cargo_subtype_text, cargo_subtype) {
    0: return string(STR_REFIT_EUM260);
    1: return string(STR_REFIT_EUM320);
    return string(STR_REFIT_EUM260);
}
