/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * K7x00
 */
// Defines
SPEED_GENERAL(K7x00)
COST(K7x00)
RUNNING(K7x00)

// Template for 7x00
template tmpl_K7x00(x, y) {
    [x,     y,    8,     22,    -3,   -11]
    [x+15,  y,   22,     17,   -14,    -9]
    [x+45,  y,   32,     11,   -16,    -7]
    [x+95,  y,   22,     17,    -6,    -9]
    [x+125, y,    8,     22,    -3,   -11]
    [x+140, y,   22,     17,   -15,    -9]
    [x+175, y,   32,     11,   -17,    -7]
    [x+225, y,   22,     17,    -7,    -9]
}

// 7x00
spriteset(set_K7x00_purchase, "./src/locomotive/K7x00/K7x00.png") {
    tmpl_purchase_detail(0, 0, 50, 11, -25, -6)
}
spriteset(set_K7x00_loco_RED_WHITE_BLUE, "./src/locomotive/K7x00/K7x00.png") {   // Red & White & Blue (current)
    tmpl_K7x00(60, 0)
}
spriteset(set_K7x00_loco_BLUE_WHITE, "./src/locomotive/K7x00/K7x00.png") {   // Blue & White
    tmpl_K7x00(60, 90)
}
spriteset(set_K7x00_loco_GREEN_YELLOW, "./src/locomotive/K7x00/K7x00.png") {   // Green & Yellow
    tmpl_K7x00(60, 120)
}
spriteset(set_K7x00_loco_TIGER, "./src/locomotive/K7x00/K7x00.png") {   // Black & Orange
    tmpl_K7x00(60, 60)
}
spriteset(set_K7x00_loco_CARGO, "./src/locomotive/K7x00/K7x00.png") {   // Cargo
    tmpl_K7x00(60, 150)
}
spriteset(set_K7x00_loco_HAERANG, "./src/locomotive/K7x00/K7x00.png") {   // Haerang
    tmpl_K7x00(60, 180)
}
spriteset(set_K7x00_loco_GTRAIN, "./src/locomotive/K7x00/K7x00.png") {   // G-train
    tmpl_K7x00(60, 210)
}
spriteset(set_K7x00_loco_STRAIN, "./src/locomotive/K7x00/K7x00.png") {   // S-train
    tmpl_K7x00(60, 240)
}

// 7600 cargo
spriteset(set_K7600_loco_cargo, "./src/locomotive/K7x00/K7x00.png") {
    tmpl_loco(60, 270)
}

// Refit Paintings
switch(FEAT_TRAINS, SELF, sw_K7x00_loco, cargo_subtype) {
    0:    set_K7x00_loco_RED_WHITE_BLUE;
    1:    set_K7x00_loco_CARGO;
    2:    set_K7x00_loco_GREEN_YELLOW;
    3:    set_K7x00_loco_BLUE_WHITE;
    4:    set_K7x00_loco_TIGER;
    // 5:    set_ERRSPRITE;    // RESERVED
    6:    set_K7x00_loco_HAERANG;
    // 7:    set_ERRSPRITE;    // RESERVED
    // 8:    set_ERRSPRITE;    // RESERVED
    // 9:    set_ERRSPRITE;    // RESERVED
    10:   set_K7x00_loco_STRAIN;
    11:   set_K7x00_loco_STRAIN;
    12:   set_K7x00_loco_GTRAIN;
    set_K7x00_loco_RED_WHITE_BLUE;
}
switch(FEAT_TRAINS, SELF, sw_K7x00_cargo_subtype_text, cargo_subtype) {
    0:    return string(STR_REFIT_LIVERY_THESEDAYS);
    1:    return string(STR_REFIT_LIVERY_7x00_CARGO);
    2:    return string(STR_REFIT_LIVERY_GREEN_YELLOW);
    3:    return string(STR_REFIT_LIVERY_7x00_FIRST_KORAIL);
    4:    return string(STR_REFIT_LIVERY_7x00_TIGER);
    // 5:    return string(STR_REFIT_IMPOSSIBLE);    // RESERVED
    6:    return string(STR_REFIT_LIVERY_HAERANG);
    // 7:    return string(STR_REFIT_IMPOSSIBLE);    // RESERVED
    // 8:    return string(STR_REFIT_IMPOSSIBLE);    // RESERVED
    // 9:    return string(STR_REFIT_IMPOSSIBLE);    // RESERVED
    10:   return string(STR_REFIT_LIVERY_STRAIN1);
    11:   return string(STR_REFIT_LIVERY_STRAIN2);
    12:   return string(STR_REFIT_LIVERY_GTRAIN);
    return string(STR_REFIT_LIVERY_THESEDAYS);
}

// item
item(FEAT_TRAINS, ko_train_K7x00, 5471) {
    property {
        // Common properties
        name:                           string(STR_K7x00_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_K7x00_INTRODUCTION;
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_DIESEL;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;

        // Spec
        speed:                          150 km/h;
        power:                          POWER_IN_KW(var_K7x00_POWER);
        weight:                         var_K7x00_WEIGHT;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.01;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_DIESEL;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_K7x00_loco;
        purchase:                       set_K7x00_purchase;

        speed:                          sw_K7x00_speed;
        cost_factor:                    sw_K7x00_cost;
        running_cost_factor:            sw_K7x00_runningcost;
        cargo_capacity:                 0;

        can_attach_wagon:               sw_Kxxxx_attach;
        cargo_subtype_text:             sw_K7x00_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
}
// End of "K7x00"
