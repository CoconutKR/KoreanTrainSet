/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * EUM
 */
SPEED_GENERAL(EUM)
COST(EUM)
RUNNING(EUM)
// CAPACITY(EUM)
CAPACITY_PURCHASE(EUM)
LOADING(EUM)

// Graphics
#include "EUM_graphic.pnml"

// Switches
#include "EUM_switch.pnml"

// Item
item(FEAT_TRAINS, ko_train_EUM, 5554) {
    property {
        // Common properties
        name:                           string(STR_EUM_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_EUM_INTRODUCTION;
        track_type:                     ELRL;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    1;

        // Spec
        speed:                          var_EUM_SPEED km/h;
        power:                          var_EUM_POWER kW;
        weight:                         35 ton;
        extra_weight_per_wagon:         35 ton;
        tractive_effort_coefficient:    0.4;
        air_drag_coefficient:           0.01;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_DIESEL;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_EUM_engine;
        purchase:                       set_EUM_purchase;

        speed:                          sw_EUM_speed;
        cost_factor:                    sw_EUM_cost;
        running_cost_factor:            sw_EUM_runningcost;
        cargo_capacity:                 sw_EUM_capacity_in_custom;
        purchase_cargo_capacity:        sw_EUM_purchase_capacity;
        loading_speed:                  sw_EUM_loadingspeed;

        can_attach_wagon:               sw_EUM_attach;
        start_stop:                     sw_ko_train_start_stop;
        additional_text:                string(STR_REFIT_ADDITIONAL_UNABLE);
    }
    // KTX-EUM: TC+M'+M+M+M'+TC, so all cars are powered car. Hence, we give a power to nonpower car, too.
    livery_override(ko_train_PASSWG) {
        default:                        sw_EUM_power_wagon;
        cargo_capacity:                 sw_EUM_capacity_in_custom;
        loading_speed:                  sw_EUM_loadingspeed;
        purchase_cargo_capacity:        sw_KTX_wagon_purchase_capacity;
        power:                          POWER_IN_KW(200);
    }
    livery_override(ko_train_PASSWGP) {
        default:                        sw_EUM_power_wagon;
        cargo_capacity:                 sw_EUM_capacity_in_custom;
        loading_speed:                  sw_EUM_loadingspeed;
        purchase_cargo_capacity:        sw_KTX_wagon_purchase_capacity;
        power:                          POWER_IN_KW(200);
    }
}
// End of "EUM"
