/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * Generator car
 */
//SPEED_GENERAL(POWER_CAR)
//         COST(POWER_CAR)
//      RUNNING(POWER_CAR)
//     CAPACITY(POWER_CAR)
//      LOADING(POWER_CAR)

spriteset(set_POWER_wagon_purchase, "./src/wagon/Power/Power.png") {
    tmpl_purchase_detail(45, 30, 32, 12, -16, -6)
}

spriteset(set_POWER_wagon_A, "./src/wagon/Power/Power.png") {
	tmpl_wagon_by_coor(0, 0)
}
spriteset(set_POWER_wagon_B, "./src/wagon/Power/Power.png") {
	tmpl_wagon_by_coor(0, 30)
}
spriteset(set_POWER_wagon_GREEN_YELLOW, "./src/wagon/Power/Power.png") {
	tmpl_wagon_by_coor(0, 60)
}
spriteset(set_POWER_wagon_BLUE_YELLOW, "./src/wagon/Power/Power.png") {
	tmpl_wagon_by_coor(0, 90)
}
spriteset(set_POWER_CAR_wagon_ATRAIN, "./src/locomotive/K7x00/A-train_wagon.png") {   // A-train's power car
    tmpl_wagon_by_coor(0, 0)
}

// Refit
switch(FEAT_TRAINS, SELF, sw_POWER_wagon_by_general, cargo_subtype) {
	1:	set_POWER_wagon_BLUE_YELLOW;
	2:	set_POWER_wagon_GREEN_YELLOW;
	3:	set_POWER_wagon_B;
	4:	set_POWER_wagon_A;
	6:	set_HAERANG_POWER_CAR;
	10:	set_STRAIN_POWER_CAR1;
	11:	set_STRAIN_POWER_CAR2;
	12:	set_GTRAIN_POWER_CAR;
	set_POWER_wagon_B;
}
switch(FEAT_TRAINS, SELF, sw_K7600_POWER_CAR_wagon, cargo_subtype) {
    13:    set_POWER_CAR_wagon_ATRAIN;
    sw_POWER_wagon_by_general;
}
switch(FEAT_TRAINS, PARENT, sw_POWER_wagon, vehicle_type_id) {
    ko_train_K7600: sw_K7600_POWER_CAR_wagon;
    sw_POWER_wagon_by_general;
}
switch(FEAT_TRAINS, SELF, sw_POWER_wagon_subtype_text_by_general, cargo_subtype) {
	1:	return string(STR_REFIT_PAINTING_POWER_CAR_SME_B);
	2:	return string(STR_REFIT_PAINTING_POWER_CAR_SME_A);
	3:	return string(STR_REFIT_PAINTING_POWER_CAR_LONG);
	4:	return string(STR_REFIT_PAINTING_POWER_CAR_DEFAULT);
	6:	return string(STR_REFIT_PAINTING_HAERANG);
	10:	return string(STR_REFIT_PAINTING_STRAIN1);
	11:	return string(STR_REFIT_PAINTING_STRAIN2);
	12:	return string(STR_REFIT_PAINTING_GTRAIN);
	return  string(STR_REFIT_PAINTING_POWER_CAR_LONG);
}
switch(FEAT_TRAINS, PARENT, sw_POWER_wagon_subtype_text, vehicle_type_id) {
    ko_train_K7600: sw_K7600_cargo_subtype_text;
    sw_POWER_wagon_subtype_text_by_general;
}

item(FEAT_TRAINS, ko_train_POWER_CAR, 59980) {
    property {
        // Common properties
        name:                           string(STR_POWER_CAR_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_POWER_CAR_INTRODUCTION;
        track_type:                     RAIL;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;

        // Spec
        power:                          0 kW;
        weight:                         17 ton;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];
        loading_speed:                  5;

        // Costs
        cost_factor:                    10;
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            150;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_POWER_wagon;
        purchase:                       set_POWER_wagon_purchase;

        cost_factor:                    var_POWER_CAR_COST;
        running_cost_factor:            var_POWER_CAR_RUNNINGCOST;
        cargo_capacity:                 var_POWER_CAR_CAPACITY;
        loading_speed:                  var_POWER_CAR_LOADINGSPEED;

        cargo_subtype_text:             sw_POWER_wagon_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
}