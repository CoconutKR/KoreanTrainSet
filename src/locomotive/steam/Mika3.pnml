/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * MIKA3 2
 */
SPEED_GENERAL(MIKA3)
         COST(MIKA3)
      RUNNING(MIKA3)
     CAPACITY(MIKA3)
      LOADING(MIKA3)

// Template
template tmpl_MIKA3(x, y) {
    [x,     y,    8,     22,    -3,   -10]
    [x+15,  y,    22,    17,   -14,    -9]
    [x+45,  y,    31,    12,   -18,    -8]
    [x+95,  y,    22,    18,    -6,    -9]
    [x+125, y,    8,     22,    -3,   -10]
    [x+140, y,    22,    18,   -16,    -9]
    [x+175, y,    31,    12,   -15,    -8]
    [x+225, y,    22,    17,    -7,   -10]
}
spriteset(set_MIKA3_purchase, "./src/locomotive/steam/steam.png") {
    tmpl_purchase_detail(0, 240, 50, 12, -25, -6)
}
spriteset(set_MIKA3_engine_1, "./src/locomotive/steam/steam.png") {
    tmpl_MIKA3(60, 240)
}
spriteset(set_MIKA3_engine_2, "./src/locomotive/steam/steam.png") {
    tmpl_MIKA3(60, 270)
}
spriteset(set_MIKA3_engine_3, "./src/locomotive/steam/steam.png") {
    tmpl_MIKA3(60, 300)
}
spriteset(set_MIKA3_engine_4, "./src/locomotive/steam/steam.png") {
    tmpl_MIKA3(60, 330)
}


// MIKA3's engine
switch(FEAT_TRAINS, SELF, sw_MIKA3_engine_by_motion, motion_counter % 4) {
    1: set_MIKA3_engine_1;
    2: set_MIKA3_engine_2;
    3: set_MIKA3_engine_3;
    set_MIKA3_engine_4;
}

switch(FEAT_TRAINS, SELF, sw_MIKA3_engine, position_in_vehid_chain % 2) {
    1: set_TENDER_2;
    sw_MIKA3_engine_by_motion;
}

// MIKA3's articulated part
switch(FEAT_TRAINS, SELF, sw_MIKA3_articulated_part, extra_callback_info1) {
    1: ko_train_MIKA3;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Item
item(FEAT_TRAINS, ko_train_MIKA3, 5301) {
    property {
        // Common properties
        name:                           string(STR_MIKA3_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_MIKA3_INTRODUCTION;
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_STEAM;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    0;

        // Spec
        speed:                          var_MIKA3_SPEED km/h;
        power:                          POWER_IN_KW(var_MIKA3_POWER);
        extra_power_per_wagon:          0 kW;
        weight:                         113 ton;
        tractive_effort_coefficient:    0.2;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   50;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 0;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_STEAM;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER);
    }
    graphics {
        default:                        sw_MIKA3_engine;
        articulated_part:               sw_MIKA3_articulated_part;
        purchase:                       set_MIKA3_purchase;

        length:                         sw_TENDER_length;

        speed:                          sw_MIKA3_speed;
        cost_factor:                    sw_MIKA3_cost;
        running_cost_factor:            sw_MIKA3_runningcost;
        cargo_capacity:                 sw_MIKA3_capacity;
        loading_speed:                  sw_MIKA3_loadingspeed;

        additional_text:                string(STR_REFIT_ADDITIONAL_UNABLE);
    }
}
// End of "MIKA3"