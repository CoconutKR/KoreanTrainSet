/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * SEOUL_METRO_1
 */
SPEED_METRO(SEOUL_METRO_1)
       COST(SEOUL_METRO_1)
    RUNNING(SEOUL_METRO_1)
   CAPACITY(SEOUL_METRO_1)
    LOADING(SEOUL_METRO_1)
//SPEED_METRO(METRO_wagon)   // Defines the specs of metro passenger car here, too
//       COST(METRO_wagon)
//    RUNNING(METRO_wagon)
   CAPACITY(METRO_wagon)
//    LOADING(METRO_wagon)

// Graphics
#include "seoul1+_graphic.pnml"

// Switch
#include "seoul1+_switch.pnml"

// Item
item(FEAT_TRAINS, ko_train_SEOUL_METRO_1, 5711) {
    property {
        // Common properties
        name:                           string(STR_SEOUL_METRO_1_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_SEOUL_METRO_1_INTRODUCTION;
        track_type:                     ELRL;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    1;

        // Spec
        power:                          POWER_IN_KW(var_SEOUL_METRO_1_POWER);
        weight:                         20 ton;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_SEOUL_METRO_1_engine;
        purchase:                       sw_SEOUL_METRO_1_purchase;

        speed:                          sw_SEOUL_METRO_1_speed;
        cost_factor:                    sw_SEOUL_METRO_1_cost;
        running_cost_factor:            sw_SEOUL_METRO_1_runningcost;
        cargo_capacity:                 sw_SEOUL_METRO_1_capacity;
        loading_speed:                  sw_SEOUL_METRO_1_loadingspeed;

        can_attach_wagon:               sw_METRO_attach;
        start_stop:                     sw_ko_train_start_stop;
        cargo_subtype_text:             sw_SEOUL_METRO_1_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
    livery_override(ko_train_PASSWG) {
        default:                        sw_SEOUL_METRO_1_wagon;
        cargo_capacity:                 sw_METRO_wagon_capacity;
        cargo_subtype_text:             sw_SEOUL_METRO_1_cargo_subtype_text;
    }
    livery_override(ko_train_PASSWGP) {
        default:                        sw_SEOUL_METRO_1_wagon;
        cargo_capacity:                 sw_METRO_wagon_capacity;
        cargo_subtype_text:             sw_SEOUL_METRO_1_cargo_subtype_text;
        power:                          POWER_IN_KW(var_METRO_wagon_POWER);
    }
}
// End of "SEOUL_METRO_1"
