/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/

// Cargo subtype
switch(FEAT_TRAINS, SELF, sw_NURIRO_cargo_subtype_text, cargo_subtype) {
    1: return string(STR_REFIT_LIVERY_NURIRO_KOREAN_WAVE);   // Korean wave livery
    2: return string(STR_REFIT_LIVERY_O_TRAIN);              // O-train
    3: return string(STR_REFIT_LIVERY_NURIRO_SANTA);         // Donghae Santa livery
    4: return string(STR_REFIT_LIVERY_NURIRO_2022);          // 2022 livery
    return string(STR_REFIT_LIVERY_NURIRO);
}

// Donghae santa livery
switch(FEAT_TRAINS, SELF, sw_NURIRO_SANTA_engineT, num_vehs_in_consist % 2) {
    1: set_NURIRO_SANTA_engineT_B;
    set_NURIRO_SANTA_engineT_A;
}
switch(FEAT_TRAINS, SELF, sw_NURIRO_SANTA_wagon_panto, position_in_consist % 2) {
    1: set_NURIRO_SANTA_wagon_panto_A;
    set_NURIRO_SANTA_wagon_panto_B;
}

switch(FEAT_TRAINS, SELF, sw_NURIRO_SANTA_wagon, position_in_consist % 2) {
    1: set_NURIRO_SANTA_wagon_A;
    set_NURIRO_SANTA_wagon_B;
}


// Engine
switch(FEAT_TRAINS, SELF, sw_NURIRO_engineH, cargo_subtype) {
    1: set_NURIRO_KOREAN_WAVE_engineH;   // Korean wave livery
    2: set_NURIRO_O_TRAIN_engineH;       // O-train
    3: set_NURIRO_SANTA_engineH;         // Donghae Santa livery
    4: set_NURIRO_2022_engineH;          // 2022 livery
    set_NURIRO_engineH;
}
switch(FEAT_TRAINS, SELF, sw_NURIRO_engineT, cargo_subtype) {
    1: set_NURIRO_KOREAN_WAVE_engineT;   // Korean wave livery
    2: set_NURIRO_O_TRAIN_engineT;       // O-train
    3: sw_NURIRO_SANTA_engineT;          // Donghae Santa livery
    4: set_NURIRO_2022_engineT;          // 2022 livery
    set_NURIRO_engineT;
}
switch(FEAT_TRAINS, SELF, sw_NURIRO_engine, position_in_consist_from_end == 0 && num_vehs_in_consist > 1) {
    1: sw_NURIRO_engineT;
    sw_NURIRO_engineH;
}

// Wagon
switch(FEAT_TRAINS, PARENT, sw_NURIRO_wagon_panto, cargo_subtype) {
    1: set_NURIRO_KOREAN_WAVE_wagon_panto;   // Korean wave livery
    2: set_NURIRO_O_TRAIN_wagon_panto;       // O-train
    3: sw_NURIRO_SANTA_wagon_panto;          // Donghae Santa livery
    4: set_NURIRO_2022_wagon_panto;          // 2022 livery
    set_NURIRO_wagon_panto;
}
switch(FEAT_TRAINS, PARENT, sw_NURIRO_wagon_normal, cargo_subtype) {
    1: set_NURIRO_KOREAN_WAVE_wagon;   // Korean wave livery
    2: set_NURIRO_O_TRAIN_wagon;       // O-train
    3: sw_NURIRO_SANTA_wagon;          // Donghae Santa livery
    4: set_NURIRO_2022_wagon;          // 2022 livery
    set_NURIRO_wagon;
}
switch(FEAT_TRAINS, SELF, sw_NURIRO_wagon, position_in_consist + 1 == num_vehs_in_consist - 1) {
    1: sw_NURIRO_wagon_panto;
    sw_NURIRO_wagon_normal;
}

// Attach
switch(FEAT_TRAINS, SELF, sw_NURIRO_attach, vehicle_type_id) {
    ko_train_NURIRO:  return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWG:  return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWGP: return CB_RESULT_ATTACH_ALLOW;
    return string(STR_ERROR_NO_ATTACH);
}
switch(FEAT_TRAINS, SELF, sw_NURIRO_attach_global, bool_no_vehicle_attachment_restrictions) {
    1: return CB_RESULT_ATTACH_ALLOW;
    sw_NURIRO_attach;
}
