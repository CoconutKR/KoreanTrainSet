/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * Pashi5 2
 */
SPEED_GENERAL(PASHI5)
COST(PASHI5)
RUNNING(PASHI5)
CAPACITY(PASHI5)
CAPACITY_PURCHASE(PASHI5)
LOADING(PASHI5)

// Template
template tmpl_PASHI5(x, y) {
    tmpl_MATE2(x, y)
}
template tmpl_PASHI5_tender(x, y) {
    tmpl_MATE2_tender(x, y)
}
spriteset(set_PASHI5_purchase, "./src/locomotive/steam/PASHI5.png") {
    tmpl_purchase_detail(0, 0, 54, 15, -29, -9)
}
spriteset(set_PASHI5_engine_1, "./src/locomotive/steam/PASHI5.png") {
    tmpl_PASHI5(60, 0)
}
spriteset(set_PASHI5_engine_2, "./src/locomotive/steam/PASHI5.png") {
    tmpl_PASHI5(60, 30)
}
spriteset(set_PASHI5_engine_3, "./src/locomotive/steam/PASHI5.png") {
    tmpl_PASHI5(60, 60)
}
spriteset(set_PASHI5_engine_4, "./src/locomotive/steam/PASHI5.png") {
    tmpl_PASHI5(60, 90)
}
spriteset(set_PASHI5_tender, "./src/locomotive/steam/PASHI5.png") {
    tmpl_PASHI5_tender(60, 120)
}


// PASHI5's engine
switch(FEAT_TRAINS, SELF, sw_PASHI5_engine_by_motion, motion_counter % 4) {
    1: set_PASHI5_engine_1;
    2: set_PASHI5_engine_2;
    3: set_PASHI5_engine_3;
    set_PASHI5_engine_4;
}

switch(FEAT_TRAINS, SELF, sw_PASHI5_engine, position_in_vehid_chain % 2) {
    1: set_PASHI5_tender;
    sw_PASHI5_engine_by_motion;
}

// PASHI5's articulated part
switch(FEAT_TRAINS, SELF, sw_PASHI5_articulated_part, extra_callback_info1) {
    1: ko_train_PASHI5;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Length
switch(FEAT_TRAINS, SELF, sw_PASHI5_length, position_in_vehid_chain % 2) {
    1: return 5;
    return 8;
}

// Item
item(FEAT_TRAINS, ko_train_PASHI5, 5302) {
    property {
        // Common properties
        name:                           string(STR_PASHI5_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_PASHI5_INTRODUCTION;
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_STEAM;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    0;

        // Spec
        speed:                          var_PASHI5_SPEED km/h;
        power:                          POWER_IN_KW(var_PASHI5_POWER);
        weight:                         var_PASHI5_WEIGHT;
        tractive_effort_coefficient:    0.2;
        air_drag_coefficient:           0.01;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   50;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 0;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_STEAM;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER);
    }
    graphics {
        default:                        sw_PASHI5_engine;
        articulated_part:               sw_PASHI5_articulated_part;
        purchase:                       set_PASHI5_purchase;

        length:                         sw_PASHI5_length;

        speed:                          sw_PASHI5_speed;
        cost_factor:                    sw_PASHI5_cost;
        running_cost_factor:            sw_PASHI5_runningcost;
        cargo_capacity:                 sw_PASHI5_capacity;
        purchase_cargo_capacity:        sw_PASHI5_purchase_capacity;
        loading_speed:                  sw_PASHI5_loadingspeed;

        can_attach_wagon:               sw_steam_attach;

        additional_text:                string(STR_REFIT_ADDITIONAL_UNABLE);
    }
}
// End of "PASHI5"
