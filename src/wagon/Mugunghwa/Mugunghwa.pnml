/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
// Defines
GENERAL(MUGUNGHWA_wagon)

spriteset(set_MUGUNGHWA_wagon_purchase, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_purchase_detail(45, 150, 32, 12, -16, -8)
}

// A: 1970~1983
spriteset(set_MUGUNGHWA_wagon_A, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 0)
}

// B: 1984~1984
spriteset(set_MUGUNGHWA_wagon_B, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 30)
}

// C: 1985~1989
spriteset(set_MUGUNGHWA_wagon_C, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 60)
}

// D: 1990~1999
spriteset(set_MUGUNGHWA_wagon_D, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 90)
}

// E: 2000~2002
spriteset(set_MUGUNGHWA_wagon_E, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 120)
}

// Current Korail paint
spriteset(set_MUGUNGHWA_wagon_RED_BLUE_WHITE, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 150)
}

// Old express (구특전)
spriteset(set_MUGUNGHWA_wagon_OLD_EXPRESS, "./src/wagon/Mugunghwa/Mugunghwa.png") {
    tmpl_wagon_by_coor(0, 180)
}

// Refit
switch(FEAT_TRAINS, SELF, sw_MUGUNGHWA_wagon, cargo_subtype) {
    1: set_MUGUNGHWA_wagon_A;
    2: set_MUGUNGHWA_wagon_B;
    3: set_MUGUNGHWA_wagon_C;
    4: set_MUGUNGHWA_wagon_D;
    5: set_MUGUNGHWA_wagon_E;
    6: set_MUGUNGHWA_wagon_OLD_EXPRESS;
	set_MUGUNGHWA_wagon_RED_BLUE_WHITE;
}
switch(FEAT_TRAINS, SELF, sw_MUGUNGHWA_wagon_subtype_text, cargo_subtype) {
    1: return string(STR_REFIT_PAINTING_1970_1983);
    2: return string(STR_REFIT_PAINTING_1984_1984);
    3: return string(STR_REFIT_PAINTING_1985_1989);
    4: return string(STR_REFIT_PAINTING_1990_1999);
    5: return string(STR_REFIT_PAINTING_2000_2002);
    6: return string(STR_REFIT_PAINTING_OLD_EXPRESS);
	return string(STR_REFIT_PAINTING_THESEDAYS);
}

// Not allow to attach except for Kxxxx series
switch(FEAT_TRAINS, PARENT, sw_MUGUNGHWA_wagon_start_stop, vehicle_type_id) {
	ko_train_K4x00:      return CB_RESULT_ATTACH_ALLOW;
	ko_train_K7x00:      return CB_RESULT_ATTACH_ALLOW;
	ko_train_K7000:      return CB_RESULT_ATTACH_ALLOW;
	ko_train_K8000:      return CB_RESULT_ATTACH_ALLOW;
	ko_train_K8100n8200: return CB_RESULT_ATTACH_ALLOW;
	ko_train_K8500:      return CB_RESULT_ATTACH_ALLOW;
	return CB_RESULT_NO_TEXT;
}

item(FEAT_TRAINS, ko_train_MUGUNGHWA_CAR, 55940) {
    property {
        // Common properties
        name:                           string(STR_MUGUNGHWA_CAR_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_MUGUNGHWA_wagon_INTRODUCTION;
        track_type:                     RAIL;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;

        // Spec
        power:                          0 kW;
        weight:                         17 ton;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];
        loading_speed:                  5;

        // Costs
        cost_factor:                    10;
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            150;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_MUGUNGHWA_wagon;
        purchase:                       set_MUGUNGHWA_wagon_purchase;

        cost_factor:                    var_MUGUNGHWA_wagon_COST;
        running_cost_factor:            var_MUGUNGHWA_wagon_RUNNINGCOST;
        cargo_capacity:                 var_MUGUNGHWA_wagon_CAPACITY;
        loading_speed:                  var_MUGUNGHWA_wagon_LOADINGSPEED;

        can_attach_wagon:               sw_GENE_attach;
        start_stop:                     sw_MUGUNGHWA_wagon_start_stop;

        cargo_subtype_text:             sw_MUGUNGHWA_wagon_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
}