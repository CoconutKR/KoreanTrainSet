/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * K7600
 */
SPEED_GENERAL(K7600)
COST(K7600)
RUNNING(K7600)

// Template
template tmpl_K7600(x, y) {
    [x,     y,    8,     22,    -3,   -12]
    [x+15,  y,    21,    17,    -14,   -8]
    [x+45,  y,    30,    12,    -17,   -8]
    [x+95,  y,    21,    17,    -6,    -8]
    [x+125, y,    8,     22,    -3,   -12]
    [x+140, y,    21,    17,    -14,   -8]
    [x+175, y,    30,    12,    -17,   -8]
    [x+225, y,    21,    17,    -6,    -8]
}
spriteset(set_K7600_loco_CARGO, "./src/locomotive/K7x00/K7600.png") {   // Cargo livery
    tmpl_K7600(60, 0)
}
spriteset(set_K7600_loco_ATRAIN, "./src/locomotive/K7x00/K7600.png") {   // A-train (Jeongseon Arirang)
    tmpl_K7600(60, 30)
}
spriteset(set_K7600_purchase, "./src/locomotive/K7x00/K7600.png") {
    tmpl_purchase_detail(0, 0, 50, 12, -25, -6)
}

// Refit Paintings
switch(FEAT_TRAINS, SELF, sw_K7600_loco, cargo_subtype) {
    13:    set_K7600_loco_ATRAIN;
    set_K7600_loco_CARGO;
}
switch(FEAT_TRAINS, SELF, sw_K7600_cargo_subtype_text, cargo_subtype) {
    13:    return string(STR_REFIT_PAINTING_K7600_ATRAIN);
    return string(STR_REFIT_PAINTING_7x00_CARGO);
}

// Item
item(FEAT_TRAINS, ko_train_K7600, 5476) {
    property {
        // Common properties
        name:                           string(STR_K7600_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_K7600_INTRODUCTION;
        track_type:                     RAIL;
        engine_class:                   ENGINE_CLASS_DIESEL;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;

        // Spec
        speed:                          150 km/h;
        power:                          POWER_IN_KW(var_K7600_POWER);
        weight:                         132 ton;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_DIESEL;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_K7600_loco;
        purchase:                       set_K7600_purchase;

        speed:                          sw_K7600_speed;
        cost_factor:                    sw_K7600_cost;
        running_cost_factor:            sw_K7600_runningcost;
        cargo_capacity:                 0;

        can_attach_wagon:               sw_Kxxxx_attach;
        cargo_subtype_text:             sw_K7600_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
}
// End of "K7600"
